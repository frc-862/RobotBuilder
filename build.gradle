plugins {
    id 'java'
    id 'jacoco'
    id 'maven'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'maven-publish'
    id 'net.ltgt.errorprone' version '0.0.10'
    id 'edu.wpi.first.wpilib.versioning.WPILibVersioningPlugin' version '2.0'
}

if (!hasProperty('releaseType')) {
    WPILibVersion {
        releaseType = 'dev'
    }
}

def pubVersion = ''
if (project.hasProperty("publishVersion")) {
    pubVersion = project.publishVersion
} else {
    pubVersion = WPILibVersion.version
}

configurations.errorprone {
    resolutionStrategy.force 'com.google.errorprone:error_prone_core:2.0.9'
}

jacoco {
    toolVersion = "0.7.5.201505241946"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

mainClassName = "robotbuilder.RobotBuilder"

if (!hasProperty('mainClass')) {
    ext.mainClass = mainClassName
}

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io" // used to grab JGraph from its Github releases because it's not in maven central
    }
}


dependencies {
    compile 'org.apache.velocity:velocity:1.7'
    compile 'org.yaml:snakeyaml:1.10'
    compile 'com.github.jgraph:jgraphx:v3.3.1.1'
    compile 'org.projectlombok:lombok:1.16.18'
    testCompile 'junit:junit:4.11'
}

test {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

jar {

    from(configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }

    manifest {
        attributes('Implementation-Title': 'RobotBuilder',
                    'Implementation-Version': WPILibVersion.version,
                    'Built-By': System.getProperty('user.name'),
                    'Built-Date': new Date(),
                    'Built-JDK': System.getProperty('java.version'))
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact(shadowJar) {
                classifier null
            }
            groupId 'edu.wpi.first.wpilib'
            artifactId 'RobotBuilder'
            version pubVersion
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}
